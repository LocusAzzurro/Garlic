apply plugin: 'java'

repositories {
    mavenCentral()
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
}

dependencies {
    compileOnly 'net.md-5:bungeecord-api:1.18-R0.1-SNAPSHOT'
}

def identifier = System.env.VERSION_IDENTIFIER

group = 'org.teacon'
version = rootProject.teacon.modVersion.get() + (identifier ? '-' + identifier : '')
archivesBaseName = 'Garlic-Bungee-1.18'

java {
    toolchain.languageVersion = JavaLanguageVersion.of(17)
}

processResources {
    def props = [version: version]
    inputs.properties props
    expand props
    filteringCharset 'UTF-8'
    filesMatching('bungee.yml') {
        expand props
    }
}

tasks.register('githubActionOutput') {
    onlyIf {
        System.env.GITHUB_ACTIONS
    }
    doLast {
        println "::set-output name=artifact_name::${jar.archiveFileName.get()}"
        println "::set-output name=artifact_publish_name::${archivesBaseName}-${version}.jar"
        println "::set-output name=artifact_path::${jar.archiveFile.get().asFile.absolutePath}"
    }
}
